import com.amazonaws.services.lambda.model.InvocationType
import jp.classmethod.aws.gradle.lambda.AWSLambdaInvokeTask
import jp.classmethod.aws.gradle.lambda.AWSLambdaMigrateFunctionTask

buildscript {
    ext {
        kotlinVersion = '1.3.31'
    }
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.1.5.RELEASE'
        classpath "com.github.jengelman.gradle.plugins:shadow:5.0.0"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlinVersion"
        classpath "jp.classmethod.aws:gradle-aws-plugin:0.37"
    }
}

version '1.0-SNAPSHOT'

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'groovy'
apply plugin: 'jp.classmethod.aws'
apply plugin: 'jp.classmethod.aws.lambda'
apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'

apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

repositories {
    mavenCentral()
}

dependencies { 
    compile 'com.amazonaws:aws-lambda-java-core:1.2.0'
    compile 'com.amazonaws:aws-lambda-java-events:2.2.6'
    compile 'com.amazonaws:aws-java-sdk-s3:1.11.557'

    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile "org.jetbrains.kotlin:kotlin-reflect"

//    compile 'org.springframework.cloud:spring-cloud-starter-function-web:2.1.0.RELEASE'
    compile 'org.springframework.cloud:spring-cloud-function-adapter-aws:2.1.0.RELEASE'

    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'net.bytebuddy:byte-buddy:1.6.4'
    testCompile 'org.objenesis:objenesis:1.2'
    testCompile 'org.spockframework:spock-core:1.1-groovy-2.4'
    testCompile 'org.spockframework:spock-spring:1.1-groovy-2.4'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

shadowJar {
    classifier = 'aws'
    mergeServiceFiles()
    append 'META-INF/spring.handlers'
    append 'META-INF/spring.schemas'
    append 'META-INF/spring.tooling'
    transform(com.github.jengelman.gradle.plugins.shadow.transformers.PropertiesFileTransformer) {
        paths = ['META-INF/spring.factories' ]
        mergeStrategy = "append"
    }
}

assemble.dependsOn shadowJar

task deployFunction(type: AWSLambdaMigrateFunctionTask, dependsOn: [assemble, test]) {
//    functionName = project.name
    functionName = "aws-lambda-kotlin-number-reverse"
    runtime = com.amazonaws.services.lambda.model.Runtime.Java8
    role = "arn:aws:iam::${aws.accountId}:role/lambdaDemoRole"

    zipFile = shadowJar.archivePath
    handler = "com.shunya.reverse.Handler"
    memorySize = 512
    timeout = 60
    environment = [
            SPRING_PROFILES_ACTIVE: "dev,aws",
            MAIN_CLASS: "com.shunya.reverse.LambdaApp"
    ]
}

task invokeFunction(type: AWSLambdaInvokeTask) {
//    functionName = project.name
    functionName = "aws-lambda-kotlin-number-reverse"
    invocationType = InvocationType.RequestResponse
    payload = file("src/test/resources/sample_request.json")
    doLast {
        println "Lambda function result: " + new String(invokeResult.payload.array())
    }
}
